;;; spl-mode.el --- Simple Programming Language

;;; Commentary:
;;; .sl files contain Simple Programming Language codes

;;; Code:

(require 'find-lisp)
(require 'font-lock)
(require 'imenu)
(require 'json)
(require 'sclang-mode)
(require 'thingatpt)

(defcustom spl-buffer "*spl*"
  "The name of the spl process buffer."
  :type 'string)

(defcustom spl-interpreter (list "spl" "sc" "tcpServer" "--port=3010")
  "The name of the spl interpreter."
  :type '(repeat string))

(defvar spl-directory nil
  "The Spl directory (default=nil).")

(defun spl-start-interpeter ()
  "Start the spl process.

If `spl-interpreter' is not already a subprocess it is
started and a new window is created to display the results of
evaluating spl expressions.  Input and output is via `spl-buffer'."
  (interactive)
  (if (comint-check-proc spl-buffer)
      (spl-see-spl)
    (apply
     'make-comint
     "spl"
     (car spl-interpreter)
     nil
     (cdr spl-interpreter))
    (spl-see-spl)))

(defun spl-see-spl ()
 "Show spl output."
 (interactive)
  (if (not (comint-check-proc spl-buffer))
      (spl-start-interpeter)
   (delete-other-windows)
   (split-window-vertically)
   (with-current-buffer spl-buffer
     (let ((window (display-buffer (current-buffer))))
       (goto-char (point-max))
       (save-selected-window
         (set-window-point window (point-max)))))))

(defun spl-find-files (dir rgx)
  "Find files at DIR matching RGX."
  (let ((case-fold-search nil))
    (mapc (lambda (filename)
            (find-file-other-window filename))
          (find-lisp-find-files dir rgx))))

(defun spl-help ()
  "Lookup up the name at point in the Spl help files."
  (interactive)
  (let ((rgx (concat "^" (thing-at-point 'symbol) "\\.help\\.sl$")))
    (spl-find-files (concat spl-directory "Help/") rgx)))

(defun spl-write-text-file (filename text)
  "Write TEXT to FILENAME"
  (write-region text nil filename))

(defun spl-netcat-cmd (cmd key value)
  "Send command CMD with the parameter KEY = VALUE to the Spl server."
  (let ((filename "/tmp/spl-netcat.json")
	(text (json-encode `((command . ,cmd) (,key . ,value)))))
    (spl-write-text-file filename text)
    (shell-command (format "ncat 127.0.0.1 3010 < %s" filename)))) ; netcat -q 0 -C

(defun spl-eval-current-file ()
  "Evaluate current file."
  (interactive)
  (spl-netcat-cmd 'evalFile 'fileName buffer-file-name))


(defun spl-trim-leading-whitespace (str)
  "Remove whitespace at the beginning of STR."
  (save-match-data
    (if (string-match "\\`[ \t\n\r]+" str)
        (replace-match "" t t str)
      str)))

(defun spl-delete-markdown-code-fences (str)
  "Remove Mardown code fences from the string STR if present.

Delete any leading whitespace this creates."
  (spl-trim-leading-whitespace
   (replace-regexp-in-string "^```.*$\\|~~~.*$" "" str)))

(defun spl-delete-doctest-prefixes (str)
  "Remove Doctest prefixes from the string STR if present.

If the string begins with a doctest, also delete all non doctest lines."
  (if (char-equal (string-to-char str) ?>)
      (setq str (replace-regexp-in-string "^[^>].*" "" str)))
  (replace-regexp-in-string "^[> ]*" "" str))

(defun spl-set-region-to-paragraph ()
  "Set the mark at the start and point at the end of the current paragraph."
  (interactive)
  (backward-paragraph)
  (push-mark nil t t)
  (forward-paragraph))

(defun spl-get-text (start end)
  "Get the text from start to end as a string, with code fences deleted."
  (spl-delete-doctest-prefixes
   (spl-delete-markdown-code-fences
    (buffer-substring-no-properties start end))))

(defun spl-get-paragraph ()
  "Get the current paragraph as a string, with code fences deleted."
  (spl-set-region-to-paragraph)
  (spl-get-text (region-beginning) (region-end)))

(defun spl-uncomment (s)
  "Remove initial comment and Bird-literate markers from the string S if present."
   (replace-regexp-in-string "^[- ]*[> ]*" "" s))

(defun spl-get-line ()
  "Get the current line as a string."
  (spl-get-text
   (line-beginning-position)
   (line-end-position)))

(defun spl-get-region ()
  "Get the current region as a string."
  (spl-get-text
   (region-beginning)
   (region-end)))

(defun spl-eval-string (STR)
  "Evaluate string."
  (spl-netcat-cmd 'evalText 'text STR))

(defun spl-eval-paragraph ()
  "Evaluate paragraph."
  (interactive)
  (spl-eval-string (spl-get-paragraph)))

(defun spl-eval-line ()
  "Evaluate line."
  (interactive)
  (spl-eval-string (spl-get-line))
  (if (/= (line-end-position) (point-max))
      (forward-line 1)))

(defun spl-eval-region ()
  "Evaluate the region."
  (interactive)
  (spl-eval-string (spl-get-region)))

(defun spl-eval-region-or-line ()
  "Evaluate region if selected, else current line."
  (interactive)
  (if (and transient-mark-mode mark-active)
      (spl-eval-region)
    (spl-eval-line)))

(defun spl-play-paragraph ()
  "Play Ugen graph of current paragraph."
  (interactive)
  (spl-eval-string (format "{ %s }.value.play" (spl-get-paragraph))))

(defun spl-draw-paragraph ()
  "Draw current paragraph."
  (interactive)
  (spl-eval-string (format "{ %s }.value.drawing.show" (spl-get-paragraph))))

(defun spl-print-promise ()
  "Print Promise of current paragraph when it resolves."
  (interactive)
  (spl-eval-string (format "{ %s }.value.then { :answer | answer.postLine }" (spl-get-paragraph))))

(defun spl-plot-paragraph ()
  "Plot Ugen graph of current paragraph."
  (interactive)
  (spl-eval-string (format "{ %s }.value.soundPlot(0.1)" (spl-get-paragraph))))

(defun spl-print-paragraph ()
  "Print Ugen graph of current paragraph."
  (interactive)
  (spl-eval-string (format "{ %s }.value.print" (spl-get-paragraph))))

(defun spl-play-current-file ()
  "Play current file at Spl server."
  (interactive)
  (spl-netcat-cmd 'playFile 'fileName buffer-file-name))

(defun spl-indent-paragraph ()
  "Indent current paragraph one TAB stop to the right."
  (interactive)
  (spl-set-region-to-paragraph)
  (indent-rigidly-right-to-tab-stop (region-beginning) (region-end)))

(defun spl-unindent-paragraph ()
  "Indent current paragraph one TAB stop to the left."
  (interactive)
  (spl-set-region-to-paragraph)
  (indent-rigidly-left-to-tab-stop (region-beginning) (region-end)))

(defun spl-clear-clock ()
  (interactive)
  (spl-eval-string "system.clock.removeAll"))

(defun spl-reset-scsynth ()
  (interactive)
  (spl-eval-string "system.scSynth.reset"))

(defun spl-stop ()
  (interactive)
  (spl-clear-clock)
  (spl-reset-scsynth))

(defun spl-exit ()
  (interactive)
  (spl-eval-string "system.exit(0)"))

(defun spl-insert-non-local-return ()
  (interactive)
  (insert "valueWithReturn { :return:/1 |"))

(defvar spl-mode-hook
  nil
  "Hook to run on entering spl-mode.")

(defvar spl-imenu-generic-expression
  (list
   (list nil "^\\(/\\* .* \\*/\\)$" 1) ; Region Comment (C)
   ;; (list nil "^\\(-- .*\\)$" 1) ; Line Comment
   (list nil "^\\(#+ .*\\)$" 1) ; Heading
   (list nil "^\\(\+?@?\[?@?[A-Z][, A-Za-z0-9@]+[A-Za-z0-9]\]?\\).* {\\( |\\|$\\)" 1)
   (list nil "^\\(\t\[?[, *&@|~+/%><=?!#^a-zA-Z0-9-]+\]?\\) {" 1))
  "Value for `imenu-generic-expression' in Spl mode.")

(defconst spl-font-lock-keywords
  (list
   `(,(regexp-opt '("let" "var") 'symbols) . font-lock-keyword-face)
   `(,(regexp-opt '("false" "nil" "true" "system") 'symbols) . font-lock-builtin-face)
   `(,(regexp-opt '("do" "doWhileFalse" "doWhileTrue" "if" "ifTrue" "ifFalse" "ifEmpty" "ifNotEmpty" "ifNil" "ifNotNil" "kr" "timesRepeat" "whileFalse" "whileTrue" "withIndexDo") 'symbols) . font-lock-function-name-face)
   `(,(regexp-opt '("error" "warning") 'symbols) . font-lock-warning-face)
   '("\\<[A-Z][a-zA-Z0-9]*\\>" . font-lock-type-face)
   '(" :[a-z][a-zA-Z0-9]*" . 'font-lock-variable-name-face)
   ;'("`[a-zA-Z]*`" . 'font-lock-constant-face)
   '("primitive:" . 'font-lock-warning-face))
  "Spl font-lock rules.")

;(defun spl-font-lock-syntactic-face (state)
;  (cond ((eq (nth 3 state) ?') 'font-lock-string-face)
;	((nth 3 state) 'font-lock-constant-face)
;	((nth 4 state) 'font-lock-comment-face)))

(defun spl-fill-syntax-table (st)
  "Modify the syntax table ST for Spl."
  (modify-syntax-entry ?\  " " st) ; whitespace
  (modify-syntax-entry ?\t " " st) ; whitespace
  (modify-syntax-entry ?\( "()" st) ; open parenthesis
  (modify-syntax-entry ?\) ")(" st) ; close parenthesis
  (modify-syntax-entry ?\[ "(]" st) ; open parenthesis
  (modify-syntax-entry ?\] ")[" st) ; close parenthesis
  (modify-syntax-entry ?\{ "(}" st) ; open parenthesis
  (modify-syntax-entry ?\} "){" st) ; close parenthesis
  (modify-syntax-entry ?* ". 23n" st) ; punctuation & comment
  (modify-syntax-entry ?/ ". 14b" st) ; punctuation & comment
  ;; (modify-syntax-entry ?\n ">" st) ; comment end
  (modify-syntax-entry ?\' "\"" st) ; string quote
  ;; (modify-syntax-entry ?\" "\"" st) ; string quote
  ;; (modify-syntax-entry ?\` "\"" st) ; string (symbol) quote (interacts badly with code fences)
  (modify-syntax-entry ?\\ "\\" st) ; escape
  (mapc (lambda (x)
          (modify-syntax-entry x "." st)) ; punctuation only
        "!#$%&+-.:<=>?@^|~,;\\")
  st)

(defvar spl-mode-syntax-table
  (spl-fill-syntax-table (make-syntax-table))
  "Syntax table for spl-mode")

(defun spl-prettier ()
  "Unicode symbols"
  (setq prettify-symbols-compose-predicate
	(lambda (start end match)
	  (if (string-match-p "\\[\\|\\]\\|{\\|}"  match)
	      t
	    (prettify-symbols-default-compose-p start end match))))
  (setq
   prettify-symbols-alist
   '(
     ("[]" . ?‚ç¨) ; Apl ; U+236C ‚ç¨ APL Functional Symbol Zilde
     ("ceiling" . ?‚åà) ; Apl
     ("doubleFactorial" . ?‚Äº) ; U+203C ‚Äº Double Exclamation Mark
     ("factorial" . ?!) ; Apl
     ("floor" . ?‚åä) ; Apl
     ("gradeDown" . ?‚çí) ; Apl
     ("gradeUp" . ?‚çã) ; Apl
     ("identity" . ?‚ä£) ; Apl ; U+22A3 ‚ä£ Left Tack
     ("innerProduct" . ?‚Äß) ; Apl ; U+2027 ‚Äß Hyphenation Point ; U+25AA ‚ñ™ Black Small Square
     ("iota" . ?‚ç≥) ; Apl ; U+2373 ‚ç≥ APL Functional Symbol Iota
     ("leftIdentity" . ?‚ä£) ; Apl ; U+22A3 ‚ä£ Left Tack
     ("log" . ?‚çü) ; Apl ; U+235F ‚çü Apl Functional Symbol Circle Star
     ("max" . ?‚åà) ; Apl ; U+2308 ‚åà Left Ceiling
     ("min" . ?‚åä) ; Apl ; U+230A ‚åä Left Floor
     ("negate" . ?-) ; Apl
     ("nthRoot" . ?‚àö) ; Apl
     ("over" . ?‚ç•) ; Apl
     ("reciprocal" . ?√∑) ; Apl
     ("reshape" . ?‚ç¥) ; Apl
     ("rightIdentity" . ?‚ä¢) ; Apl ; U+22A2 ‚ä¢ Right Tack
     ("rotateLeft" . ?‚åΩ) ; Apl
     ("shape" . ?‚ç¥) ; Apl ; U+2374 ‚ç¥ Apl Functional Symbol Rho
     ("swap" . ?‚ç®) ; Apl ; U+2368 ‚ç® APL Functional Symbol Tilde Diaeresis
     ;("bind" . ?‚àò) ; Apl
     ;("conjugate" . ?+) ; Apl & J
     ;("constant" . ?‚ç®) ; Apl
     ;("drop" . ?‚Üì) ; Apl
     ;("enclose" . ?‚äÇ) ; Apl
     ;("exp" . ?*) ; Apl (Not √ó, C.f. sign, J=^)
     ;("expand" . ?\\) ; Apl
     ;("find" . ?‚ç∑) ; Apl
     ;("foldRight" . ?/) ; Apl
     ;("nest" . ?‚äÜ) ; Apl
     ;("nubSieve" . ?‚â†) ; Apl
     ;("partition" . ?‚äÜ) ; Apl
     ;("power" . ?*) ; Apl
     ;("reduce" . ?/) ; Apl
     ;("replicateEach" . ?/) ; Apl
     ;("sign" . ?√ó) ; Apl (Not *, C.f. exp, J=*)
     ;("take" . ?‚Üë) ; Apl
     ;("tally" . ?‚â¢) ; Apl
     ;("unique" . ?‚à™) ; Apl ; U+2191 ‚Üë Upwards Arrow; U+222A ‚à™ Union
     ;("windowedReduce" . ?/) ; Apl
     ("!!" . ?‚Äº) ; U+203C ‚Äº Double Exclamation Mark
     ("!~" . ?‚ââ) ; U+2249 ‚ââ Not Almost Equal To ; ‚âÅ
     ("*" . ?√ó) ; U+00D7 √ó Multiplication Sign
     ("**" . ?‚áà) ; U+21C8 ‚áà Upwards Paired Arrows
     ("*.x" . ?‚äó) ; U+2297 ‚äó Circled Times
     ("//" . ?‚´Ω) ; U+02AFD ‚´Ω Double Solidus Operator
     ("\\\\" . ?‚ëä) ; U+244A ‚ëä OCR Double Backslash
     ("+-" . ?¬±) ; U+00B1 ¬± Plus-Minus Sign
     ("++" . ?‚ß∫) ; U+29FA ‚ß∫ Double plus
     ("+++" . ?‚ßª) ; U+29FB ‚ßª Triple Plus
     ("-" . ?‚àí) ; U+2212 ‚àí Minus Sign
     ("-<" . ?‚§ô) ; U+2919 ‚§ô Leftwards Arrow-Tail
     ("->" . ?‚Üí) ; U+2190 ‚Üí Rightwards Arrow
     ("->>" . ?‚Ü†) ; U+21A0 ‚Ü† Rightwards Two Headed Arrow
     ("--" . ?‚Äî) ; U+2014 ‚Äî Em Dash; U+02013 ‚Äì En Dash
     ("---" . ?‚∏∫) ; U+02E3A ‚∏∫ Two-Em Dash
     ("-->" . ?‚ü∂) ; U+027F6 ‚ü∂ Long Rightwards Arrow
     (".." . ?‚Ä¶) ; U+2026 ‚Ä¶ Horizontal Ellipsis; U+2025 ‚Ä• Two Dot Leader
     ("/" . ?√∑) ; U+00F7 √∑ Division Sign
     (":." . ?‚Ä£) ; U+2023 ‚Ä£ Triangular Bullet
     (":/" . ?‚ß∏) ; U+00B0 ‚ß∏ Big Solidus
     (":/0" . ?‚ÇÄ) ; U+2080 ‚ÇÄ Subscript Zero
     (":/1" . ?‚ÇÅ) ; U+2081 ‚ÇÅ Subscript One
     (":/2" . ?‚ÇÇ) ; U+2082 ‚ÇÇ Subscript Two
     (":/3" . ?‚ÇÉ) ; U+2083 ‚ÇÉ Subscript Three
     (":/4" . ?‚ÇÑ) ; U+2084 ‚ÇÑ Subscript Four
     (":/5" . ?‚ÇÖ) ; U+2085 ‚ÇÖ Subscript Five
     (":/6" . ?‚ÇÜ) ; U+2086 ‚ÇÜ Subscript Six
     (":/7" . ?‚Çá) ; U+2087 ‚Çá Subscript Seven
     (":/8" . ?‚Çà) ; U+2088 ‚Çà Subscript Eight
     (":/9" . ?‚Çâ) ; U+2089 ‚Çâ Subscript Nine
     ("::" . ?¬∑) ; U+00B7 ¬∑ Middle Dot ; ‚à∑
     (":=" . ?‚Üê) ; U+2190 ‚Üê Leftwards Arrow (U+2254 ‚âî Colon Equals)
     (":?" . ?‚ç∞) ; U+2370 ‚ç∞ Apl Functional Symbol Quad Question ; U+291D ‚§ù Leftwards arrow to black diamond
     (":@" . ?‚ãÑ) ; U+22C4 ‚ãÑ Diamond Operator
     ("<-" . ?‚Üê) ; U+2190 ‚Üê Leftwards Arrow
     ("<=" . ?‚â§) ; U+2264 ‚â§ Less-than or equal to
     ("<|" . ?‚â∫) ; U+227a ‚â∫ Precedes ; U+25C1 ‚óÅ White Left-Pointing Triangle
     ("<=|" . ?‚âº) ; U+227c ‚âº Precedes Or Equal To
     ("<~" . ?‚™Ö) ; U+2A85 ‚™Ö Less-Than or Approximate ; ‚™ù
     ("<<" . ?‚â™) ; U+226A ‚â™ Much Less-Than ; U+00AB ¬´ Left-Pointing Double Angle Quotation Mark
     ("<<<" . ?‚ãò) ; U+22D8 ‚ãò Very Much Less-Than Unicode Character
     ("==" . ?‚â°) ; U+2261 ‚â° Identical To
     ("=>" . ?‚áí) ; U+21D2 ‚áí Rightwards Double Arrow
     (">=" . ?‚â•) ; U+2265 ‚â• Greater-than or equal to
     (">~" . ?‚™Ü) ; U+2A86 ‚™Ü Greater-Than or Approximate ; ‚™û
     (">>" . ?‚â´) ; U+226B ‚â´ Much Greater-Than ; U+00BB ¬ª Right-Pointing Double Angle Quotation Mark
     (">>>" . ?‚ãô) ; U+22D9 ‚ãô Very Much Greater-Than
     ("|>" . ?‚âª) ; U+227B ‚âª Succeeds ; U+25B7 ‚ñ∑ White Right-Pointing Triangle
     ("|>=" . ?‚âΩ) ; U+227D ‚âΩ Succeeds or Equal To
     ;("/*" . ?¬´) ; U+000AB ¬´ Left-Pointing Double Angle Quotation Mark
     ;("*/" . ?¬ª) ; U+000BB ¬ª Right-Pointing Double Angle Quotation Mark
     ("Phi" . ?œï) ; U+03D5 œï Greek Phi Symbol
     ("Psi" . ?Œ®) ; U+03A8 Œ® Greek Capital Letter Psi
     ("Sqrt" . ?‚àö) ; U+221A ‚àö Square Root
     ("Sum" . ?‚àë) ; U+03A3 Œ£ Greek Capital Letter Sigma ; U+2211 ‚àë N-Ary Summation
     ("^" . ?ÀÜ) ; U+02C6 ÀÜ Modifier Letter Circumflex Accent ; U+2191 ‚Üë Upwards Arrow
     ("alpha" . ?Œ±) ; U+03B1 Œ± Greek Small Letter Alpha
     ("and" . ?‚àß) ; U+2227 ‚àß Logical and
     ("beta" . ?Œ≤) ; U+03B2 Œ≤ Greek Small Letter Beta
     ("cancelFlat" . ?‚ôÆ) ; U+266E ‚ôÆ Music Natural Sign
     ("cancelSharp" . ?‚ôÆ) ; U+266E ‚ôÆ Music Natural Sign
     ("complement" . ?‚àÅ) ; U+2201 ‚àÅ Complement
     ("compose" . ?‚àò) ;  U+2218 ‚àò Ring Operator
     ("catenate" . ?‚ß∫) ; U+29FA ‚ß∫ Double plus, U+29FB ‚ßª Triple Plus
     ("concatenation" . ?‚ß∫) ; U+29FA ‚ß∫ Double plus
     ("Chi" . ?Œß) ; U+03A7 Œß Greek Capital Letter Chi
     ("chi" . ?œá) ; U+03C7 œá Greek Small Letter Chi
     ("crossedMultiply" . ?‚äó) ; U+2297 ‚äó Circled Times
     ("degree" . ?¬∞) ; U+00b0 ¬∞ Degree Sign
     ("delta" . ?Œ¥) ; U+03B4 Œ¥ Greek Small Letter Delta
     ("difference" . ?‚àñ) ; U+2216 ‚àñ Set Minus
     ("doesNotInclude" . ?‚àå) ; Unicode: U+220C ‚àå Does not contain as member
     ("duplicate" . ?!)
     ("each" . ?‚çµ) ; U+2375 ‚çµ APL Functional Symbol Omega
     ("epsilon" . ?Œµ) ; U+03B5 Œµ Greek Small Letter Epsilon
     ("false" . ?‚ä•) ; U+22A5 ‚ä• Up tack
     ("first" . ?‚ÇÅ) ; U+2081 ‚ÇÅ Subscript One
     ("flat" . ?‚ô≠) ; U+266D ‚ô≠ Music Flat Sign
     ("fourth" . ?‚ÇÑ) ; U+2084 ‚ÇÑ Subscript Four
     ("gamma" . ?Œ≥) ; U+03B3 Œ≥ Greek Small Letter Gamma ; U+1D6FE ùõæ Mathematical Italic Small Gamma
     ("Gamma" . ?Œì) ; U+0393 Œì Greek Capital Letter Gamma
     ("i" . ?ùíä) ; U+1D48A ùíä Mathematical Italic Small I
     ("includes" . ?‚àã) ; U+220B ‚àã Contains as Member
     ("index" . ?‚Öà) ; U+02148 ‚Öà Double-Struck Italic Small I
     ("infinity" . ?‚àû) ; U+221E ‚àû Infinity
     ("Infinity" . ?‚àû) ; U+221E ‚àû Infinity
     ("intersection" . ?‚à©) ; U+2229 ‚à© Intersection
     ("j" . ?ùëó) ; U+1D457 ùëó Mathematical Italic Small J
     ("kappa" .?Œ∫) ; U+003BA Œ∫ Greek Small Letter Kappa
     ("lambda" . ?Œª) ; U+03BB Œª Greek Small Letter Lamda
     ("Lambda" . ?Œõ) ; U+039B Œõ Greek Capital Letter Lamda
     ("mu" . ?Œº) ; U+03BC Œº Greek Small Letter Mu
     ("micro" . ?Œº) ; U+03BC Œº Greek Small Letter Mu
     ("natural" . ?‚ôÆ) ; U+266E ‚ôÆ Music Natural Sign
     ("not" . ?¬¨) ; U+00AC ¬¨ Not sign
     ("nu" . ?ŒΩ) ; U+003BD ŒΩ Greek Small Letter Nu
     ("omega" . ?œâ) ; U+03C9 œâ Greek Small Letter Omega
     ("Omega" . ?Œ©) ; U+03A9 Œ© Greek Capital Letter Omega
     ("or" . ?‚à®) ; ‚à® U+2228 Logical Or
     ("percent" . ?%) ; Unicode: U+0025 % Percent Sign
     ("phi" . ?œÜ) ; U+03C6 œÜ Greek Small Letter Phi
     ("psi" . ?œà) ; U+03C8 œà Greek Small Letter Psi
     ("pi" . ?œÄ) ; U+03C0 œÄ Greek Small Letter Pi
     ("Pi" . ?Œ†) ; U+003A0 Œ† Greek Capital Letter Pi
     ("precedes" . ?‚â∫) ; U+227a ‚â∫ Precedes
     ("precedesOrEqualTo" . ?‚âº) ; U+227c ‚âº Precedes Or Equal To
     ("succeeds" . ?‚âª) ; U+227B ‚âª Succeeds
     ("succeedsOrEqualTo" . ?‚âΩ) ; U+227D ‚âΩ Succeeds or Equal To ; U+2AB0 ‚™∞ Succeeds Above Single-Line Equals Sign
     ("product" . ?Œ†) ; U+03A0 Œ† Greek Capital Letter Pi
     ("quarterToneFlat" . ?ùÑ≥) ; U+1D133 ùÑ≥ Musical Symbol Quarter Tone Flat
     ("quarterToneSharp" . ?ùÑ≤) ; U+1D132 ùÑ≤ Musical Symbol Quarter Tone Sharp
     ("r" . ?ùëü) ; U+1D45F ùëü Mathematical Italic Small R
     ("return" . ?‚Üë) ; U+2191 ‚Üë Upwards Arrow
     ("return:/1" . ?‚Üë) ; U+2191 ‚Üë Upwards Arrow
     ("reverse" . ?·¥ô) ; U+1D19 ·¥ô Latin Letter Small Capital Reversed R ; U+233D ‚åΩ APL Functional Symbol Circle Stile
     ("rotateLeft" . ?‚åΩ) ; U+233D ‚åΩ APL Functional Symbol Circle Stile
     ("rho" . ?œÅ) ; U+03C1 œÅ Greek Small Letter Rho
     ("second" . ?‚ÇÇ) ; U+2082 ‚ÇÇ Subscript Two
     ("sharp" . ?‚ôØ) ; U+266F ‚ôØ Music Sharp Sign
     ("sigma" . ?œÉ) ; U+03C3 œÉ Greek Small Letter Sigma
     ("Sigma" . ?Œ£) ; U+03A3 Œ£ Greek Capital Letter Sigma
     ("sqrt" . ?‚àö) ; ‚Äú‚àö‚Äù U+221A Square Root
     ("square" . ?¬≤) ; U+00B2 ¬≤ Superscript Two
     ("cube" . ?¬≥) ; U+00B3 ¬≥ Superscript Three
     ("sum" . ?Œ£) ; U+03A3 Œ£ Greek Capital Letter Sigma ; U+2211 ‚àë N-Ary Summation
     ("Tau" . ?Œ§) ; U+03A4 Œ§ Greek Capital Letter Tau
     ("tau" . ?œÑ) ; U+03C4 œÑ Greek Small Letter Tau
     ("theta" . ?Œ∏) ; U+03B8 Œ∏ Greek Small Letter Theta
     ("Theta" . ?Œò) ; U+0398 Œò Greek Capital Letter Theta
     ("third" . ?‚ÇÉ) ; U+2083 ‚ÇÉ Subscript Three
     ("transpose" . ?·µÄ) ; U+1D40 ·µÄ Modifier Letter Capital T ; U+2349 ‚çâ APL Functional Symbol Circle Backslash
     ("true" . ?‚ä§) ; U+22A4 ‚ä§ Down Tack
     ("u" . ?ùë¢) ; U+1D462 ùë¢ Mathematical Italic Small U
     ("undefined" . ?‚ä•) ; U+22A5 ‚ä• Up tack
     ("union" . ?‚à™) ; U+222A ‚à™ Union
     ("valueWithReturn" . ?‚Üì) ; U+2193 ‚Üì Downwards Arrow
     ("xi" . ?Œæ) ; U+03BE Œæ Greek Small Letter Xi
     ("Xi" . ?Œû) ; U+039E Œû Greek Capital Letter Xi
     ("Zeta" . ?Œñ) ; U+0396 Œñ Greek Capital Letter Zeta
     ("zeta" . ?Œ∂) ; U+03B6 Œ∂ Greek Small Letter Zeta
     ("||" . ?‚Äñ) ; U+2016 ‚Äñ Double Vertical Line
     ("~" . ?‚âà) ; U+2248 ‚âà Almost Equal To
     ("!=" . ?‚â†) ; U+2260 ‚â† Not equal to
     ("~>" . ?‚áù) ; U+21DD ‚áù Rightwards Squiggle Arrow
     ("!==" . ?‚â¢) ; U+2262 ‚â¢ Not Identical To
     (":;" . ?‚Ü≤) ; U+21B2 ‚Ü≤ Downwards Arrow With Tip Leftwards
     ("Sha" . ?–®) ; U+0428 –® Cyrillic Capital Letter Sha
     ("sha" . ?—à) ; U+0448 —à Cyrillic Small Letter Sha
     ("halve" . ?¬Ω) ; U+00BD ¬Ω Vulgar Fraction One Half

     ("[|" . ?‚ü¶) ; U+27E6 ‚ü¶ Mathematical Left White Square Bracket
     ("|]" . ?‚üß) ; U+27E7 ‚üß Mathematical Right White Square Bracket

     ("{|" . ?‚¶É) ; U+2983 ‚¶É Left White Curly Bracket
     ("|}" . ?‚¶Ñ) ; U+2984 ‚¶Ñ Right White Curly Bracket

     ;; ("--" . ?‚çù) ; U+235D ‚çù APL Functional Symbol Up Shoe Jot ; U+2014 ‚Äî Em Dash
     ;; (")|" . ?‚¶à) ; U+2988 ‚¶à Z Notation Right Image Bracket ; ‚¶Ñ ; ‚∏°
     ;; ("..." . ?‚Ä•) ; U+2026 ‚Ä¶ Horizontal Ellipsis; U+22ef ‚ãØ Midline Horizontal Ellipsis
     ;; ("e" . ?ùëí)
     ;; ("|(" . ?‚¶á) ; U+2989 ‚¶á Z Notation Left Image Bracket
     )))

(defun spl-fill-mode-map (map)
  "Install Spl keybindings into MAP."
  (define-key map (kbd "C-c >") 'spl-see-spl)
  (define-key map (kbd "C-c C-h") 'spl-help)
  (define-key map (kbd "C-c C-a") 'spl-play-paragraph)
  (define-key map (kbd "C-c C-g") 'spl-draw-paragraph)
  (define-key map (kbd "C-c C-p") 'spl-print-promise)
  (define-key map (kbd "C-c C-e") 'spl-eval-paragraph)
  (define-key map (kbd "C-c C-c") 'spl-eval-region-or-line)
  (define-key map (kbd "C-c C-k") 'spl-reset-scsynth)
  (define-key map (kbd "C-c C-s") 'spl-stop)
  (define-key map (kbd "C-c C-r") 'spl-insert-non-local-return)
  (define-key map (kbd "C-c C-i") 'spl-indent-paragraph)
  (define-key map (kbd "C-c C-q") 'spl-exit)
  map)

(defvar spl-mode-map
  (spl-fill-mode-map (make-sparse-keymap))
  "Keymap used in Spl mode.")

(defcustom spl-indent-level
  8
  "Indentation for Spl."
  :type '(integer))

(defun spl-mode-set-local-variables ()
  (set (make-local-variable 'indent-tabs-mode) t)
  (set (make-local-variable 'tab-width) spl-indent-level)
  (set (make-local-variable 'sclang-indent-level) spl-indent-level)
  (set (make-local-variable 'indent-line-function) 'sclang-indent-line)
  (set (make-local-variable 'comment-start) "/* ")
  (set (make-local-variable 'comment-end) " */")
  ;(set (make-local-variable 'font-lock-syntactic-face-function) 'spl-font-lock-syntactic-face)
  (set (make-local-variable 'font-lock-defaults) '(spl-font-lock-keywords))
  (setq-local imenu-sort-function 'imenu--sort-by-name)
  (setq-local imenu-generic-expression spl-imenu-generic-expression))

(define-derived-mode spl-mode prog-mode "Spl"
  "Major mode for editing Simple Programming Language files."
  :group 'spl
  :syntax-table spl-mode-syntax-table
  (spl-mode-set-local-variables))

(add-to-list 'auto-mode-alist '("\\.sl$" . spl-mode))
(add-to-list 'auto-mode-alist '("\\.sp$" . spl-mode))
(add-to-list 'auto-mode-alist '("\\.help.sl$" . spl-mode))

(defun spl-indent-region ()
  "Run spl-indent on region."
  (interactive)
  (shell-command-on-region (region-beginning) (region-end) "spl-indent" nil t))

(defun spl-fix-category ()
  (interactive)
  (shell-command-on-region (point-min) (point-max) "/home/rohan/sw/spl/hs/fixCategory" t t))

(defun spl-to-doctest ()
  (interactive)
  (shell-command-on-region (point-min) (point-max) "/home/rohan/sw/spl/hs/toDoctest" t t))

(define-skeleton spl-mathematica-reference-skeleton
  "Insert a Mathematica reference."
  "Ref: "
  "References:\n"
  "_Mathematica_\n"
  "[1](https://mathworld.wolfram.com/" str | " *** Ref *** " ".html)\n")

(define-skeleton spl-supercollider-reference-skeleton
  "Insert a SuperCollider reference."
  "Ref: "
  "References:\n"
  "_SuperCollider_\n"
  "[1](https://doc.sccode.org/Classes/" str | " *** Ref *** " ".html)\n")

(define-skeleton spl-haskell-reference-skeleton
  "Insert a Haskell Prelude reference."
  "Ref: "
  "References:\n"
  "_Haskell_\n"
  "[1](https://hackage.haskell.org/package/base/docs/Prelude.html#v:" str | " *** Ref *** " ")\n")

(define-skeleton spl-image-reference-skeleton
  "Insert an image reference."
  "Ref: "
  "![](sw/spl/Help/Image/" str ".svg)\n")

(provide 'spl-mode)

;;; spl.el ends here
