;;; spl-mode.el --- Simple Programming Language

;;; Commentary:
;;; .sl files contain Simple Programming Language codes

;;; Code:

(require 'find-lisp)
(require 'font-lock)
(require 'imenu)
(require 'json)
(require 'sclang-mode)
(require 'thingatpt)

(defvar spl-directory nil
  "The Spl directory (default=nil).")

(defvar stsc3-directory nil
  "The StSc3 directory (default=nil).")

(defun spl-find-files (dir rgx)
  "Find files at DIR matching RGX."
  (mapc (lambda (filename)
          (find-file-other-window filename))
        (find-lisp-find-files dir rgx)))

(defun spl-help ()
  "Lookup up the name at point in the Spl help files."
  (interactive)
  (let ((rgx (concat "^" (thing-at-point 'symbol) "\\.help\\.sl$")))
    (spl-find-files (concat spl-directory "help/") rgx)
    (spl-find-files (concat stsc3-directory "help/") rgx)))

(defun spl-write-text-file (filename text)
  "Write TEXT to FILENAME"
  (write-region text nil filename))

(defun spl-netcat-cmd (cmd key value)
  "Send command CMD with the parameter KEY = VALUE to the Spl server."
  (let ((filename "/tmp/spl-netcat.json")
	(text (json-encode `((command . ,cmd) (,key . ,value)))))
    (spl-write-text-file filename text)
    (shell-command (format "ncat 127.0.0.1 3010 < %s" filename)))) ; netcat -q 0 -C

(defun spl-delete-markdown-code-fences (str)
  "Remove Mardown code fences from the string STR if present."
  (replace-regexp-in-string "^```" "" str))

(defun spl-set-region-to-paragraph ()
  "Set the mark at the start and point at the end of the current paragraph."
  (interactive)
  (backward-paragraph)
  ; (recenter t)
  (push-mark nil t t)
  (forward-paragraph))

(defun spl-get-text (start end)
  "Get the text from start to end as a string, with code fences deleted."
  (spl-delete-markdown-code-fences
   (buffer-substring-no-properties start end)))

(defun spl-get-paragraph ()
  "Get the currently paragraph as a string, with code fences deleted."
  (spl-set-region-to-paragraph)
  (spl-get-text (region-beginning) (region-end)))

(defun spl-eval-paragraph ()
  "Evaluate paragraph at Spl server."
  (interactive)
  (spl-netcat-cmd 'evalText 'text (spl-get-paragraph)))

(defun spl-print-ugen-graph-of-paragraph ()
  "Evaluate .printUgenGraph at Spl server."
  (interactive)
  (spl-netcat-cmd 'evalText 'text (format "{ %s }.value.printUgenGraph" (spl-get-paragraph))))

(defun spl-eval-current-file ()
  "Load current file at Spl server."
  (interactive)
  (spl-netcat-cmd 'evalFile 'fileName buffer-file-name))

(defun spl-play-paragraph ()
  "Play Ugen graph of current paragraph."
  (interactive)
  (spl-netcat-cmd 'evalText 'text (format "{ %s }.play" (spl-get-paragraph))))

(defun spl-draw-paragraph ()
  "Draw Ugen graph of current paragraph."
  (interactive)
  (spl-netcat-cmd 'evalText 'text (format "{ %s }.drawUgenGraph" (spl-get-paragraph))))

(defun spl-print-paragraph ()
  "Print Ugen graph of current paragraph."
  (interactive)
  (spl-netcat-cmd 'evalText 'text (format "{ %s }.value.printUgenGraph" (spl-get-paragraph))))

(defun spl-play-current-file ()
  "Play current file at Spl server."
  (interactive)
  (spl-netcat-cmd 'playFile 'fileName buffer-file-name))

(defun spl-clear-clock ()
  (interactive)
  (spl-netcat-cmd 'evalText 'text "system.clock.removeAll"))

(defun spl-reset-scsynth ()
  (interactive)
  (spl-netcat-cmd 'evalText 'text "system.scSynth.reset"))

(defun spl-stop ()
  (interactive)
  (spl-clear-clock)
  (spl-reset-scsynth))

(defun spl-insert-non-local-return ()
  (interactive)
  (insert "valueWithReturn { :return:/1 |"))

(defvar spl-mode-hook
  nil
  "Hook to run on entering spl-mode.")

(defvar spl-imenu-generic-expression
  (list
   (list nil "^\\((\\* .*\\*)\\)$" 1)
   (list nil "^\\(#+ .*\\)$" 1)
   (list nil "^\\(\+?@?\[?[A-Z][, A-Za-z0-9]+[A-Za-z0-9]\]?\\).* {\\( |\\|$\\)" 1)
   (list nil "^\\(\t[*&|~+/%><=?!^a-zA-Z0-9-]+\\) {" 1))
  "Value for `imenu-generic-expression' in Spl mode.")

(defconst spl-font-lock-keywords
  (list
   `(,(regexp-opt '("var") 'symbols) . font-lock-keyword-face)
   `(,(regexp-opt '("false" "inf" "nil" "pi" "e" "epsilon" "true") 'symbols) . font-lock-builtin-face)
   `(,(regexp-opt '("do" "if" "ifEmpty" "ifFalse" "ifNil" "ifNotNil" "ifTrue" "kr" "timesRepeat" "whileFalse" "whileTrue") 'symbols) . font-lock-function-name-face)
   `(,(regexp-opt '("error" "warn") 'symbols) . font-lock-warning-face)
   '("\\<[A-Z][a-zA-Z0-9]*\\>" . font-lock-type-face)
   '(":[a-z][a-zA-Z0-9]*" . 'font-lock-variable-name-face)
   '("primitive:" . 'font-lock-warning-face))
  "Spl font-lock rules.")

(defun spl-fill-syntax-table (st)
  "Modify the syntax table ST for Spl."
  (modify-syntax-entry ?\* ". 23n" st)
  (modify-syntax-entry ?\( "()1" st)
  (modify-syntax-entry ?\) ")(4" st)
  (modify-syntax-entry ?' "\"" st)
  (modify-syntax-entry ?\" "\"" st)
  ;;(modify-syntax-entry ?\; ". 12b" st)
  (modify-syntax-entry ?\n "> b" st)
  st)

(defvar spl-mode-syntax-table
  (spl-fill-syntax-table (make-syntax-table))
  "Syntax table for spl-mode")

(defun spl-prettier ()
  "Unicode symbols"
  (setq
   prettify-symbols-alist
   '(("return:/1" . ?â†‘) ; U+2191 â†‘ Upwards Arrow
     ("return" . ?â†‘) ; U+2191 â†‘ Upwards Arrow
     ("valueWithReturn" . ?â†“) ; U+2193 â†“ Downwards Arrow
     ("->" . ?â†’) ; U+2190 â†’ Rightwards Arrow
     (":=" . ?â†) ; U+2190 â† Leftwards Arrow (U+2254 â‰” Colon Equals)
     (":/" . ?â§¸) ; U+00B0 â§¸ Big Solidus
     ("::" . ?Â·) ; U+00B7 Â· Middle Dot
     (":." . ?â€£)
     (":@" . ?â‹„) ; U+22C4 â‹„ Diamond Operator
     ("~=" . ?â‰ ) ; U+2260 â‰  Not equal to
     ("==" . ?â‰¡) ; U+2261 â‰¡ Identical To
     ("~~" . ?â‰¢) ; U+2262 â‰¢ Not Identical To
     ("<=" . ?â‰¤) ; U+2264 â‰¤ Less-than or equal to
     (">=" . ?â‰¥) ; U+2265 â‰¥ Greater-than or equal to
     ("++" . ?â§º) ; U+29FA â§º Double plus
     ("concatenation" .?â§») ; U+29FB â§» Triple Plus
     ("transposed" .?áµ€) ; U+1D40 áµ€ Modifier Letter Capital T
     ("reversed" .?á´™) ; U+1D19 á´™ Latin Letter Small Capital Reversed R
     ("pi" . ?Ï€) ; U+03C0 Ï€ Greek Small Letter Pi
     ("epsilon" . ?Îµ) ; U+03B5 Îµ Greek Small Letter Epsilon
     ;; ("e" . ?ğ‘’)
     ("*" . ?Ã—) ; U+00D7 Ã— Multiplication Sign
     ("-" . ?âˆ’) ; U+2212 âˆ’ Minus Sign
     ("^" . ?Ë†) ; U+2191 â†‘ Upwards Arrow ; U+02C6 Ë† Modifier Letter Circumflex Accent
     ("**" . ?â‡ˆ) ; U+21C8 â‡ˆ Upwards Paired Arrows
     ("/" . ?Ã·) ; U+00F7 Ã· Division Sign
     ("true" . ?âŠ¤) ; U+22A4 âŠ¤ Down Tack
     ("false" . ?âŠ¥) ; U+22A5 âŠ¥ Up tack
     ("not" . ?Â¬) ; U+00AC Â¬ Not sign
     ("and" . ?âˆ§) ; U+2227 âˆ§ Logical and
     ("or" . ?âˆ¨) ; âˆ¨ U+2228 Logical Or
     ("sum" . ?Î£) ; U+03A3 Î£ Greek Capital Letter Sigma ; U+2211 âˆ‘ N-Ary Summation
     ("product" . ?Î ) ; U+03A0 Î  Greek capital letter pi
     ("crossedMultiply" . ?âŠ—) ; U+2297 âŠ— Circled Times
     ("*.x" . ?âŠ—)
     ("Sqrt" . ?âˆš)
     ("sqrt" . ?âˆš) ; â€œâˆšâ€ U+221A Square Root
     ;;(";;" . ?â‹¯)
     ("alpha" . ?Î±)
     ("beta" . ?Î²)
     ("|(" . ?â¦‡) ; â¦ƒ â¸  â¦‡
     (")|" . ?â¦ˆ) ; â¦„ â¸¡ â¦ˆ
     ("~" . ?â‰ˆ) ; U+2248 â‰ˆ Almost Equal To
     ("!~" . ?â‰‰) ; â‰
     ("<~" . ?âª…) ; âª
     (">~" . ?âª†) ; âª
     ("union" . ?âˆª) ; U+222A âˆª Union
     ("intersection" . ?âˆ©) ; U+2229 âˆ© Intersection
     ("includes" . ?âˆ‹) ; U+220B âˆ‹ Contains as Member
     ("doesNotInclude" . ?âˆŒ) ; Unicode: U+220C âˆŒ Does not contain as member
     ("flat" . ?â™­) ; U+266D â™­ Music Flat Sign
     ("sharp" . ?â™¯) ; U+266F â™¯ Music Sharp Sign
     ("natural" . ?â™®) ; U+266E â™® Music Natural Sign
     ("cancelFlat" . ?â™®) ; U+266E â™® Music Natural Sign
     ("cancelSharp" . ?â™®) ; U+266E â™® Music Natural Sign
     ("quarterToneFlat" .?ğ„³) ; U+1D133 ğ„³ Musical Symbol Quarter Tone Flat
     ("quarterToneSharp" .?ğ„²) ; U+1D132 ğ„² Musical Symbol Quarter Tone Sharp
     (":?" .?â°) ; U+2370 â° Apl Functional Symbol Quad Question ; U+291D â¤ Leftwards arrow to black diamond
     ("||" .?â€–) ; U+2016 â€– Double Vertical Line
     (".." .?â€¥) ; U+2025 â€¥ Two Dot Leader
     ;;("..." .?â€¥) ; U+2026 â€¦ Horizontal Ellipsis; U+22ef â‹¯ Midline Horizontal Ellipsis
     ("--" .?â€”) ; U+2014 â€” Em Dash
     ("duplicate" .?!)
     ("squared" .?Â²) ; U+00B2 Â² Superscript Two
     ("first" .?â‚) ; U+2081 â‚ Subscript One
     ("second" .?â‚‚) ; U+2082 â‚‚ Subscript Two
     ("third" .?â‚ƒ) ; U+2083 â‚ƒ Subscript Three
     ("fourth" .?â‚„) ; U+2084 â‚„ Subscript Four
     ("rho" .?Ï)
     ("phi" .?Ï†)
     ("theta" .?Î¸)
     ("lambda" .?Î») ; U+03BB Î» Greek Small Letter Lamda
     ("delta" .?Î´) ; U+03B4 Î´ Greek Small Letter Delta
     ("r" .?ğ‘Ÿ) ; U+1D45F ğ‘Ÿ Mathematical Italic Small R
     )))

(defun spl-fill-mode-map (map)
  "Install Spl keybindings into MAP."
  (define-key map (kbd "C-c C-h") 'spl-help)
  (define-key map (kbd "C-c C-a") 'spl-play-paragraph)
  (define-key map (kbd "C-c C-g") 'spl-draw-paragraph)
  (define-key map (kbd "C-c C-e") 'spl-eval-paragraph)
  (define-key map (kbd "C-c C-k") 'spl-reset-scsynth)
  (define-key map (kbd "C-c C-s") 'spl-stop)
  (define-key map (kbd "C-c C-r") 'spl-insert-non-local-return)
  map)

(defvar spl-mode-map
  (spl-fill-mode-map (make-sparse-keymap))
  "Keymap used in Spl mode.")

(defcustom spl-indent-level
  8
  "Indentation for Spl."
  :type '(integer))

(defun spl-mode-set-local-variables ()
  (set (make-local-variable 'indent-tabs-mode) t)
  (set (make-local-variable 'tab-width) spl-indent-level)
  (set (make-local-variable 'sclang-indent-level) spl-indent-level)
  (set (make-local-variable 'indent-line-function) 'sclang-indent-line)
  (set (make-local-variable 'comment-start) "(* ")
  (set (make-local-variable 'comment-end) " *)")
  (set (make-local-variable 'font-lock-defaults) '(spl-font-lock-keywords))
  (setq-local imenu-sort-function 'imenu--sort-by-name)
  (setq-local imenu-generic-expression spl-imenu-generic-expression))

(define-derived-mode spl-mode prog-mode "Spl"
  "Major mode for editing Simple Programming Language files."
  :group 'spl
  :syntax-table spl-mode-syntax-table
  (spl-mode-set-local-variables))

(add-to-list 'auto-mode-alist '("\\.sl$" . spl-mode))
(add-to-list 'auto-mode-alist '("\\.help.sl$" . spl-mode))

(defun spl-indent-region ()
  "Run spl-indent on region."
  (interactive)
  (shell-command-on-region (region-beginning) (region-end) "spl-indent" nil t))

(provide 'spl-mode)

;;; spl.el ends here
