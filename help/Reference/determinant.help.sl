# determinant

- _determinant(aMatrix)_

Answer the determinant of a square matrix.

The determinant of a _2×2_ `Integer` matrix:

```
>>> [1 2; 3 4].determinant
-2

>>> [3 1; 7 -4].determinant
-19

>>> [1 2; 0 2].determinant
2

>>> [4 3; 2 5].determinant
14

>>> [1 4; -1 9].determinant
13
```

The determinants of _3×3_ `Integer` matrices:

```
>>> [1 2 3; 4 5 6; 7 8 9].determinant
0

>>> [-1 4 -2; -2 -4 3; 6 -5 1].determinant
1

>>> [1 2 4; 5 4 5; 9 2 7].determinant
-66

>>> [-2 -1 2; 2 1 4; -3 3 -1].determinant
54

>>> [1 2 3; 4 1 6; 7 8 1].transposed.determinant
104

>>> [2 9 4; 7 5 3; 6 1 8].determinant
-360
```

The determinants of _4×4_ `Integer` matrices:

```
>>> [1 2 3 4; 4 5 6 7; 7 8 9 10; 10 11 12 13].determinant
0

>>> [1 2 3 1; -1 -1 -1 2; 1 3 1 1; -2 -2 0 -1].determinant
26

>>> [7 2 -2 4; 4 4 1 7; 11 -8 9 10; 10 5 12 13].determinant
-4319
```

The determinant of a _5×5_ `Integer` matrix:

```
>>> ([5 5].iota - 1).determinant
0
```

The determinant of a _3×3_ `SmallFloat` matrix:

```
>>> [
>>> 	1.7 7.1 -2.7;
>>> 	2.2 8.7 3.2;
>>> 	3.2 -9.2 1.2
>>> ].determinant
251.572
```

The determinant of a _3×3_ `Complex` matrix:

```
>>> [
>>> 	1j1 2j0 3j-2;
>>> 	0j0 12.56637j0 0j5;
>>> 	3j0 0j0 6j0
>>> ].determinant
-37.6991j180.79644
```

An `identityMatrix` always has unit determinant:

```
>>> 2.identityMatrix.determinant
1

>>> 3.identityMatrix.determinant
1
```

Use to find area of a parallelogram spanned by _(1, 4)_ and _(-5, 2)_:

```
>>> [1 4; -5 2].determinant.abs
22
```

Use to find the volume of a parallelepiped:

```
>>> [1 4 3; -2 -5 2; -1 2 -2].determinant.abs
45
```

Use `determinant` to find the hypervolume of a hyper-parallelepiped spanned by the following vectors:

```
>>> let m = [
>>> 	6 -6 -1 6;
>>> 	10 -1 0 -7;
>>> 	3 -2 -9 -3;
>>> 	4 5 6 -3
>>> ];
>>> m.determinant.abs
3580
```

Find the area of the image of the unit disk under the linear transformation associated to a matrix:

```
>>> let m = [7 -3; 5 7; -10 4];
>>> m.transposed.dot(m).determinant.sqrt * pi
10 * 122.sqrt * pi
```

Determine whether the following basis for _R ^3_ is right-handed:

```
>>> [1 0 1; 0 1 1; 1 0 0].determinant
-1
```

The determinant of the transpose of _m_ equals the determinant of _m_:

```
>>> let m = system.randomReal(0, 1, [3 3]);
>>> m.transposed.determinant
m.determinant
```

The determinant is a multiplicative map,
ie. the determinant of a matrix product equals the product of their determinants:

```
>>> let a = system.randomReal(0, 1, [3 3]);
>>> let b = system.randomReal(0, 1, [3 3]);
>>> a.dot(b).determinant
a.determinant * b.determinant
```

The determinant is the signed volume of the parallelepiped generated by its rows:

```
>>> [1 2 3; -1 1 0; 3 2 1].determinant
-12
```

A square matrix has an inverse if and only if its determinant is nonzero:

```
>>> [1 2 1; 1 0 2; -1 2 -3].determinant
0
```

* * *

See also: isSquareMatrix, matrixRank, permanent, permutationSymbol, rowReduce, trace

References:
_Mathematica_
[1](https://mathworld.wolfram.com/Determinant.html)
[2](https://reference.wolfram.com/language/ref/Det.html),
_W_
[1](https://en.wikipedia.org/wiki/Determinant)
